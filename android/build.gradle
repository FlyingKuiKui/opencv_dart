group 'dev.rainyl.opencv_dart'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'dev.rainyl.opencv_dart'
    }

    // Bumping the plugin compileSdk version requires all clients of this plugin
    // to bump the version in their app.
    compileSdk 33

    // Use the NDK version declared in /android/app/build.gradle file of the Flutter project.
    // Replace it with a version number if this plugin requires a specific NDK version.
    // (e.g. ndkVersion "23.1.7779620")
    ndkVersion android.ndkVersion

    // Invoke the shared CMake build with the Android Gradle Plugin.
    // externalNativeBuild {
    //     cmake {
    //         path "../src/CMakeLists.txt"

    //         // The default CMake version for the Android Gradle Plugin is 3.10.2.
    //         // https://developer.android.com/studio/projects/install-ndk#vanilla_cmake
    //         //
    //         // The Flutter tooling requires that developers have CMake 3.10 or later
    //         // installed. You should not increase this version, as doing so will cause
    //         // the plugin to fail to compile for some customers of the plugin.
    //         // version "3.10.2"
    //     }
    // }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19
    }
}

def NDK_PATH=System.env.ANDROID_NDK_HOME ?: System.env.ANDROID_NDK_ROOT
def BUILD_CMD=['conan', 'build', '.', '-b', 'missing']
if (NDK_PATH) {
    BUILD_CMD.addAll(['-c', "tools.android:ndk_path=${NDK_PATH}"])
}

task runOpenCVSetup(type: Exec) {
    // Set working directory to the parent directory of the root directory
    workingDir project.buildscript.sourceFile.parentFile.parentFile
    def CMD = BUILD_CMD + ['-pr:h', 'profiles/android-x86_64', '-o', 'output_dir=build/android/x86_64']
    commandLine CMD
    doFirst {
        println "Using NDK path: ${NDK_PATH}"
        println "Running OpenCV setup for x86_64 in ${workingDir}"
    }
}

task runOpenCVSetupArm64(type: Exec) {
    // Set working directory to the parent directory of the root directory
    workingDir project.buildscript.sourceFile.parentFile.parentFile
    def CMD = BUILD_CMD + ['-pr:h', 'profiles/android-armv8', '-o', 'output_dir=build/android/arm64-v8a']
    commandLine CMD
    doFirst {
        println "Using NDK path: ${NDK_PATH}"
        println "Running OpenCV setup for arm64-v8a in ${workingDir}"
    }
}

task runOpenCVSetupArmeabi(type: Exec) {
    // Set working directory to the parent directory of the root directory
    workingDir project.buildscript.sourceFile.parentFile.parentFile
    def CMD = BUILD_CMD + ['-pr:h', 'profiles/android-armv7', '-o', 'output_dir=build/android/armeabi-v7a']
    commandLine CMD
    doFirst {
        println "Using NDK path: ${NDK_PATH}"
        println "Running OpenCV setup for armeabi-v7a in ${workingDir}"
    }
}

def OPENCV_DART_DISABLE_AUTO_BUILD=System.env.OPENCV_DART_DISABLE_AUTO_BUILD ?: null
if (OPENCV_DART_DISABLE_AUTO_BUILD != null) {
    println "Found OPENCV_DART_DISABLE_AUTO_BUILD, skip auto build"
} else {
    println "OPENCV_DART_DISABLE_AUTO_BUILD is not set, running OpenCV setup"
    preBuild.dependsOn runOpenCVSetup, runOpenCVSetupArm64, runOpenCVSetupArmeabi
}
